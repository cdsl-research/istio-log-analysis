# 出力形式
set terminal pdf
set output '5-result-reduction-count.pdf'
set size ratio 0.7  # 出力サイズ

# 目盛り
set logscale y # 対数目盛り
set format y "10^{%L}"
set grid ytics # 主目盛り
set grid linewidth 1
set grid linetype 1
set grid linecolor 0
set xtics font ",24" offset 0,0  # 目盛りのフォントの変更
set ytics font ",24" offset 0,0  # 目盛りのフォントの変更

# 軸ラベル
# set xlabel 'Applied Method' offset 0,-1
set ylabel 'Number of Log Messages' offset -4,-1
set xlabel font ",26" # xlabelのフォントの変更
set ylabel font ",26" # ylabelのフォントの変更
set zlabel font ",26" # zlabelのフォントの変更
set lmargin 5  # ylabelが隠れる対策
set bmargin 5  # xlabelが隠れる対策

# グラフまわり
# set style fill solid  # 塗りつぶし
set style fill pattern 3 border  # モノクロ
set style data histogram
set boxwidth 0.5 relative

# 凡例
set key outside center top horizontal reverse Left
set key font ",18"    # 凡例のフォントの変更

# 矢印
set style arrow 999 size character 2,20 filled linewidth 2
set arrow 1 from 1.51,56488 to 1.25,16488 arrowstyle 999

# テキスト
set label font ",17" # labelのフォントの変更
set label 1 at 1.51,56488 "Summarized\nLog\nMessages"

# set datafile separator " "
set datafile separator "\t"  # 入力データの形式
plot [:] [1:] \
	"result.dat" using 0:($2+$3) with boxes title "Summarized Log Messages" lt -1, \
	"result.dat" using 0:($3):xtic(1) with boxes title "Original Log Messages" lt -1 
